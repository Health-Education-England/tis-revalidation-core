plugins {
  id "java"
  id "org.springframework.boot" version "2.4.1"
  id "io.spring.dependency-management" version "1.0.10.RELEASE"

  // Code quality plugins
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.1.1"
}

group = "uk.nhs.hee.tis.revalidation"
version = "0.3.0"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot starters
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-amqp"
  implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-web-services"
  testCompile group: 'junit', name: 'junit', version: '4.13.1'
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // MapStruct
  implementation "org.mapstruct:mapstruct:1.4.1.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.4.1.Final"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

  // Sentry reporting
  ext.sentryVersion = "3.2.0"
  implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
  implementation "io.sentry:sentry-logback:$sentryVersion"

  // Project specific dependencies
  implementation "com.github.javafaker:javafaker:1.0.2"
  implementation "org.glassfish.jaxb:jaxb-runtime"
  implementation "io.springfox:springfox-swagger2:3.0.0"
  implementation "io.springfox:springfox-swagger-ui:3.0.0"
  implementation "com.amazonaws:aws-java-sdk-cognitoidp:1.11.962"
}

checkstyle {
  toolVersion = "8.29"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    property "sonar.projectKey", "Health-Education-England_tis-revalidation-core"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
